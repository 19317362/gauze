include(gauze_check_emulator_running)
include(gauze_create_avd)
include(gauze_run_start_script)

function(gauze_start_android_emulator)
  hunter_add_package(Android-SDK) # ANDROID-SDK_ROOT

  set(GAUZE_DEVICE_PORT 5678)
  set(GAUZE_DEVICE_NAME "gauze_android-${CMAKE_SYSTEM_VERSION}_${CMAKE_ANDROID_ARCH_ABI}")
  set(GAUZE_ANDROID_ADB "${ANDROID-SDK_ROOT}/android-sdk/platform-tools/adb")
  set(GAUZE_ANDROID_EMULATOR "${ANDROID-SDK_ROOT}/android-sdk/tools/emulator")

  gauze_check_emulator_running(result)
  if(result)
    message("Device already is running")
    return()
  endif()

  string(COMPARE EQUAL "${CMAKE_ANDROID_ARCH_ABI}" "arm64-v8a" is_arm64_v8a)
  string(COMPARE EQUAL "${CMAKE_ANDROID_ARCH_ABI}" "armeabi-v7a" is_armeabi_v7a)
  string(COMPARE EQUAL "${CMAKE_ANDROID_ARCH_ABI}" "armeabi" is_armeabi)
  string(COMPARE EQUAL "${CMAKE_ANDROID_ARCH_ABI}" "mips" is_mips)
  string(COMPARE EQUAL "${CMAKE_ANDROID_ARCH_ABI}" "x86" is_x86)

  if(is_arm64_v8a)
    hunter_add_package(android_arm64_v8a_system_image_packer)
  elseif(is_armeabi_v7a)
    hunter_add_package(android_arm_eabi_v7a_system_image_packer)
  elseif(is_armeabi)
    hunter_add_package(android_arm_eabi_v7a_system_image_packer)
  elseif(is_mips)
    hunter_add_package(android_mips_system_image_packer)
  elseif(is_x86)
    hunter_add_package(android_intel_x86_atom_system_image_packer)
  else()
    message(FATAL_ERROR "Not supported: ${CMAKE_ANDROID_ARCH_ABI}")
  endif()

  gauze_create_avd()
  gauze_run_start_script()

  gauze_check_emulator_running(result)
  if(NOT result)
    message(FATAL_ERROR "Device is not running")
  endif()
endfunction()
